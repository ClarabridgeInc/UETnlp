/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'eliana' at '2/12/18 10:20 AM' with Gradle 2.7
 */

plugins {
    id 'java'
    id 'maven-publish'
}


repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile 'commons-io:commons-io:2.4'
    compile fileTree(dir: 'libs', include: '*.jar')
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'vn.edu.vnu.uet.nlp.uetnlp.bin.Execute'
    }
    baseName = project.name.toLowerCase()
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

publishing {
    publications {
        uetnlp(MavenPublication) {
            groupId project.group
            artifactId project.name.toLowerCase()
            version project.version

            artifact fatJar
        }
    }
    repositories {
        maven {
            url project.'nexus.repo.publish'
            credentials {
                username = project.'nexus.deploy.user'
                password = project.'nexus.deploy.password'
            }
        }
    }
}

publish.dependsOn build, fatJar